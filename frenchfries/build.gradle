apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

//variaveis para configuração do maven
group = 'com.fsantana.library' // Change this to match your package name
version = '0.0.1'



android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


//gerando source jar
//o padrão do maven necessita que a lib tenha um jar file contento o source
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

//gerando java doc
task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavaDocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavaDocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavaDocsJar
    archives generateSourcesJar
}

bintray {
    user = 'fabio-scienvitarso'
    key = 'a4d71b3546d60d840d2a78aad5994736d09fd419'
    pkg {
        repo = 'maven'
        name = 'com.fsantana.frenchfries'

        version {
            name = '0.0.1-beta'
            desc = 'Essa é uma pessoal library que junta tudo que eu acho necessario para os ' +
                    'meus projetos android'
            released  = new Date()
            vcsTag = '0.0.1'
        }
        vcsUrl = 'https://github.com/Scienvitarso/Library-french-fries.git'
        licenses = ['Apache-2.0']

    }
    configurations = ['archives']
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support:support-annotations:23.0.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
